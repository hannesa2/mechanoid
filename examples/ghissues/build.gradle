apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

def version = '3.0.0'

android {
    namespace "com.robotoworks.example.ghissues"
    defaultConfig {
        versionCode getGitCommitCount()
        minSdkVersion defaultMinSdkVersion
        compileSdk defaultCompileSdkVersion
        versionName version + "." + getGitCommitCount() + "-" + getLatestGitHash()
        targetSdkVersion defaultTargetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', 'src-gen']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            //disable automatic ndk-build
        }
    }
    buildFeatures {
        renderScript true
        aidl true
        buildConfig true
    }
    lint {
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation project(':libs:mechanoid')
    implementation "androidx.legacy:legacy-support-v4:$defaultAndoidXVersion"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation 'androidx.core:core-ktx:1.15.0'
}
